# Creating an Azure Landing Zone using Bicep

<!-- markdownlint-disable MD033 -->
<div style="width: 400px; height: 400px; overflow: hidden;">
  <img src="./.images/8th.png" alt="Bicep Advent Calendar" style="clip: rect(0px,60px,200px,0px);">
</div>
<!-- markdownlint-enable -->

## Introduction

An Azure Landing Zone is a way of designing and deploying cloud resources in Azure following best practices and guidelines. It helps you to migrate, modernize, and innovate your applications in a scalable and secure manner. An Azure Landing Zone consists of two types of subscriptions: platform landing zones and application landing zones. Platform landing zones provide shared services such as identity, connectivity, and management to the application landing zones. Application landing zones host the workloads themselves and can be customized for different needs.

We are going to focus on an application landing zone, so we will define some assumptions to make it easier to understand the scope of the landing zone we are going to deploy.

- The landing zone will use `Island Networking` as the network topology so there is no central hub network, peering or hybrid network connectivity.
- We have an existing Azure Subscription to use (we can also create this as part of the same deployment but to keep it simple we will use an existing subscription).
- We have a Service Principal created for the deployment and a couple of Microsoft Entra groups.
- We have a Management Group structure in place.

## Azure Landing Zone Deployment

The process diagram below outlines the various components and capabilities that will be deployed as part of the landing zone, these are explained in the bullet points below.

<!-- markdownlint-disable MD033 -->
<center><img src="docs/wiki/.media/vending-machine-lz.png" width="100%" /></center>
<!-- markdownlint-enable -->

-
- Tags deployed at the subscription and resource group level
- Role assignments deployed at the subscription level
- An Azure Budget deployed at the Subscription level
- Common Resource Groups `alertsRG` and `NetworkWatcherRG`
- Action Group in the `alertsRG` resource group
- Network watcher instance in the `NetworkWatcherRG` resource group
- A `network` resource group for spoke networking resources, including
  - A virtual network and associated subnets
  - Network security groups assigned to the subnets

## Breaking down the deployment

### Parameters

``` bicep
@description('The Subscription Id for the deployment.')
@maxLength(36)
param subscriptionId string = ''

@description('Whether to move the Subscription to the specified Management Group supplied in the parameter `subscriptionManagementGroupId`.')
param subscriptionManagementGroupAssociationEnabled bool = true

@description('The Management Group Id to place the subscription in.')
param subscriptionMgPlacement string = ''
```

### Subscription Placement

This module will move the subscription to the specified management group if the `subscriptionManagementGroupAssociationEnabled` parameter is set to `true` and the `subscriptionMgPlacement` parameter is not empty. This is a great way to ensure that the subscription is placed in the correct management group and can be used to ensure that the subscription is not moved to a management group that is not part of the landing zone structure.

``` bicep
// Module: Subscription Placement
module subscriptionPlacement '../../modules/subscriptionPlacement/subscriptionPlacement.bicep' = if (subscriptionManagementGroupAssociationEnabled && !empty(subscriptionMgPlacement)) {
  scope: managementGroup(subscriptionMgPlacement)
  name: 'subscriptionPlacement-${guid(deployment().name)}'
  params: {
    targetManagementGroupId: subscriptionMgPlacement
    subscriptionIds: [
      subscriptionId
    ]
  }
}

```

### Subscription Tags

This module will create tags at the subscription level, only if the tag object is not empty and will only update the tags if the `onlyUpdate` parameter is set to `true`. This is a great way to ensure that the subscription tags are not overwritten if they have been manually updated.

``` bicep
// Module: Subscription Tags
module subscriptionTags '../CARML/resources/tags/main.bicep' = if (!empty(tags)) {
  scope: subscription(subscriptionId)
  name: 'subTags-${guid(deployment().name)}'
  params: {
    subscriptionId: subscriptionId
    location: location
    onlyUpdate: true
    tags: tags
  }
}

```

### Subscription Budget

This module will create an Azure Budget at the subscription scope if the `budgets` object is not empty. This is a great way to ensure that the subscription has a budget in place to monitor and alert on costs.

``` bicep
// Module: Subscription Budget
module subscriptionbudget '../CARML/consumption/budget/main.bicep' = [for (bg, index) in budgets: if (!empty(budgets)) {
  name: take('subBudget-${guid(deployment().name)}-${index}', 64)
  scope: subscription(subscriptionId)
  params: {
    name: 'budget'
    location: location
    amount: bg.amount
    startDate: bg.startDate
    thresholds: bg.thresholds
    contactEmails: bg.contactEmails
  }
}]

```

### Role Assignments

This module will associated role assignments to the subscription if the `roleAssignments` object is not empty. This is a great way to ensure that the subscription has the correct role assignments in place. This could either be a `Service Principal` or a `Microsoft Entra AD Group`.

The `Role Assignment Parameter` is an *Array* so we are using the `for` loop to iterate through the array and create a role assignment for each item in the array.

There is logic in the module to determine if the role assignment is for a resource group or a subscription. If the `relativeScope` property contains `/resourceGroups/` then the role assignment is for a resource group, otherwise it is for the subscription.

``` bicep
// Module: Role Assignments
module roleAssignment '../CARML/authorization/role-assignment/main.bicep' = [for assignment in roleAssignments: if (roleAssignmentEnabled && !empty(roleAssignments)) {
  name: take('roleAssignments-${uniqueString(assignment.principalId)}', 64)
  params: {
    location: location
    principalId: assignment.principalId
    roleDefinitionIdOrName: assignment.definition
    subscriptionId: subscriptionId
    resourceGroupName: (contains(assignment.relativeScope, '/resourceGroups/') ? split(assignment.relativeScope, '/')[2] : '')
  }
}]

```

### Shared Resource Groups

This module will create a resource group for each item in the `commonResourceGroups` array. These common resource group names would be consistent across landing zones and would be used for shared services such as `alerts` and `network watcher`.

``` bicep
// Module: Resource Groups (Common)
module sharedResourceGroups '../resourceGroup/resourceGroups.bicep' = [for item in commonResourceGroups: {
  name: item
  scope: subscription(subscriptionId)
  params: {
    resourceGroupNames: commonResourceGroups
    location: location
    tags: tags
  }
}]

```

### Action Group

This module will create an action group in the `alertsRG` resource group if the `actionGroupEnabled` parameter is set to `true` and the `actionGroupEmails` array is not empty. This is a great way to ensure that notifications can be sent for things like `Service Health Alerts`.

We can also see that this module is pulling from the public Microsoft ACR for the [Action Group](https://github.com/Azure/bicep-registry-modules/tree/main/avm/res/insights/action-group).

``` bicep
module actionGroup 'br/public:avm-res-insights-actiongroup:0.1.1' = if (actionGroupEnabled && !empty(actionGroupEmails)) {
  name: 'actionGroup-${guid(deployment().name)}'
  scope: resourceGroup(subscriptionId, 'alertsRG')
  dependsOn: [
    sharedResourceGroups
  ]
  params: {
    location: 'Global'
    name: '${lzPrefix}${envPrefix}ActionGroup'
    groupShortName: '${lzPrefix}${envPrefix}AG'
    emailReceivers: [for email in actionGroupEmails: {
      emailAddress: email
      name: split(email, '@')[0]
      useCommonAlertSchema: true
    }]
  }
}

```

### Resource Group for Networking

This module will create a resource group for all spoke networking resources if the `virtualNetworkEnabled` parameter is set to `true`. This will thne be used by the spoke networking module.

``` bicep
// Module: Resource Groups (Network)
module resourceGroupForNetwork '../CARML/resources/resource-group/main.bicep' = if (virtualNetworkEnabled) {
  name: 'resourceGroupForNetwork-${guid(deployment().name)}'
  scope: subscription(subscriptionId)
  params: {
    name: resourceGroups.network
    location: location
    tags: tags
  }
}

```

### Network Watcher

This module will create a network watcher instance in the `NetworkWatcherRG` resource group if the `virtualNetworkEnabled` parameter is set to `true`. This is a great way to ensure that the subscription has a network watcher instance in place to monitor and troubleshoot network connectivity, if there is a virtual network that will be deployed.

``` bicep
// Module: Network Watcher
module networkWatcher '../CARML/network/network-watcher/main.bicep' = if (virtualNetworkEnabled) {
  name: 'networkWatcher-${guid(deployment().name)}'
  scope: resourceGroup(subscriptionId, 'networkWatcherRG')
  dependsOn: [
    sharedResourceGroups
  ]
  params: {
    location: location
    tags: tags
  }
}

```

### Spoke Networking

``` bicep
// Module: Spoke Networking
module spokeNetworking '../spokeNetworking/spokeNetworking.bicep' = if (virtualNetworkEnabled && !empty(addressPrefixes)) {
  scope: resourceGroup(subscriptionId, resourceGroups.network)
  name: 'spokeNetworking-${guid(deployment().name)}'
  dependsOn: [
    resourceGroupForNetwork
  ]
  params: {
    spokeNetworkName: resourceNames.virtualNetwork
    addressPrefixes: addressPrefixes
    ddosProtectionPlanId: ddosProtectionPlanId
    dnsServerIps: dnsServerIps
    nextHopIPAddress: nextHopIpAddress
    subnets: subnets
    disableBGPRoutePropagation: disableBGPRoutePropagation
    tags: tags
    location: location
  }
}

```


## Conclusion

Based on the blog posts throughout the week, we have seen how Bicep can be used to deploy a variety of Azure resources and used some of the concepts to start building out the structure of the `Main.bicep` template. Next week we will continue to build out the landing zone and start to look at some of the more advanced features of Bicep and how we can improve on what has been done so far.

### Further Reading

>- [What is an Azure Landing Zone?](https://learn.microsoft.com/en-us/azure/cloud-adoption-framework/ready/landing-zone/)